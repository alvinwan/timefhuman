%import common.WS
INT_YEAR.2: /\d{4}/
%import common.INT
%ignore WS

MONTHNAME: /(Jan(?:uary)?|Feb(?:ruary)?|Mar(?:ch)?|Apr(?:il)?|May|Jun(?:e)?|Jul(?:y)?|Aug(?:ust)?|Sep(?:tember)?|Oct(?:ober)?|Nov(?:ember)?|Dec(?:ember)?)/i
MERIDIEM: /(am|pm|a\.m\.|p\.m\.|a|p)/i
RELATIVE_DAY: /(today|tomorrow|yesterday)/i
WEEKDAY: /(Mon(?:day)?|Tue(?:sday)?|Wed(?:nesday)?|Thu(?:rsday)?|Fri(?:day)?|Sat(?:urday)?|Sun(?:day)?)/i
ORDINAL: INT /(st|nd|rd|th)/i
TIME_SHORTHAND: /(noon|midnight)/i
IN: /in/i
AGO: /ago/i
UNIT: /(minutes?|mins?|hours?|hrs?|days?|years?)/i

start: choice
     | expression+

choice: expression (CHOICE_SEP expression)+
     | month_partial_choice

month_partial_choice: MONTHNAME (ORDINAL|INT) CHOICE_SEP (ORDINAL|INT) "at" time_only

?expression: range
           | month_day_range
           | time_range_shorthand
           | datetime
           | relative_datetime
           | relative_range
           | weekday_datetime
           | date_only
           | time_only
           | relative_duration
           | duration_range_short
           | duration_choice_short
           | duration

datetime: date_only datetime_sep? time_only
        | time_only datetime_sep? date_only

datetime_sep: "at" | "on"

relative_datetime: RELATIVE_DAY time_only
                 | time_only RELATIVE_DAY

relative_range: RELATIVE_DAY (range | time_range_shorthand)
              | (range | time_range_shorthand) RELATIVE_DAY

weekday_datetime: WEEKDAY time_only
                | time_only WEEKDAY

date_only: dash_date
         | slash_date_full
         | slash_date_short
         | monthname_expression

dash_date: INT "-" INT "-" INT
slash_date_full: INT "/" INT "/" INT
slash_date_short: INT "/" INT

monthname_expression: MONTHNAME (ORDINAL|INT) ("," INT)?
                    | MONTHNAME (ORDINAL|INT) INT_YEAR

month_day_range: MONTHNAME INT "-" INT ("," INT)?

time_range_shorthand: INT "-" INT MERIDIEM

duration: INT UNIT
duration_range_short: INT "-" INT UNIT
duration_choice_short: INT CHOICE_SEP INT UNIT
relative_duration: IN INT UNIT
                 | INT UNIT AGO

range: range_value RANGE_SEP range_value
range_value: datetime | date_only | time_only

RANGE_SEP: "-" | "to"
CHOICE_SEP: "," | "or"

time_only: INT ":" INT MERIDIEM
         | INT MERIDIEM
         | INT ":" INT
         | INT
         | TIME_SHORTHAND -> time_shorthand