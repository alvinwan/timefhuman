%import common.WS
%import common.INT
%ignore WS

MONTHNAME: /(Jan(?:uary)?|Feb(?:ruary)?|Mar(?:ch)?|Apr(?:il)?|May|Jun(?:e)?|Jul(?:y)?|Aug(?:ust)?|Sep(?:tember)?|Oct(?:ober)?|Nov(?:ember)?|Dec(?:ember)?)/i
MERIDIEM: /(am|pm|a\.m\.|p\.m\.|a|p)/i
RELATIVE_DAY: /(today|tomorrow|yesterday)/i
ORDINAL: INT /(st|nd|rd|th)/i

start: expression+

?expression: range
           | month_day_range
           | time_range_shorthand
           | datetime
           | relative_datetime
           | date_only
           | time_only

datetime: date_only datetime_sep? time_only
        | time_only datetime_sep? date_only

datetime_sep: "at" | "on"

relative_datetime: RELATIVE_DAY time_only
                 | time_only RELATIVE_DAY

date_only: dash_date
         | slash_date_full
         | slash_date_short
         | monthname_expression

dash_date: INT "-" INT "-" INT
slash_date_full: INT "/" INT "/" INT
slash_date_short: INT "/" INT

monthname_expression: MONTHNAME (ORDINAL|INT) ("," INT)?

month_day_range: MONTHNAME INT "-" INT ("," INT)?

time_range_shorthand: INT "-" INT MERIDIEM

range: range_value RANGE_SEP range_value
range_value: datetime | date_only | time_only

RANGE_SEP: "-" | "to"

time_only: INT ":" INT MERIDIEM
         | INT MERIDIEM
         | INT ":" INT
         | INT