%import common.WS
%import common.INT
%ignore WS

// ----------------------
// TERMINAL DEFINITIONS
// ----------------------

// Month names as a regex token, case-insensitive
MONTHNAME: /(?i)(january|february|march|april|may|june|july|august|september|october|november|december|jan|feb|mar|apr|jun|jul|aug|sep|oct|nov|dec)/

// For weekdays, also as a single regex token, case-insensitive
WEEKDAY: /(?i)(monday|mon|tuesday|tues|tue|tu|wednesday|wed|thursday|thurs|thur|thu|friday|fri|saturday|sat|sunday|sun)/

// Meridiem token (am/pm, with optional dots)
MERIDIEM: /(?i)([ap](\.?m\.?)?)/

// Datename token (today, tomorrow, yesterday)
DATENAME: /(?i)(today|tomorrow|tmw|yesterday)/

// Timename token (noon)
TIMENAME: /(?i)(noon|midday|midnight)/

// Duration unit (minutes, hours, days, etc.)
DURATION_UNIT: /(?i)(seconds|second|secs|sec|minutes|mins|min|hours|hour|hrs|hr|h|days|day|weeks|week|wks|wk|months|month|mos|years|year)/
DURATION_UNIT_LETTER: /(?i)(s|m|h|d|w|mo|y)(?![a-z])/

// Duration number (digits like "1", or words like "an", "a", "one", "two", etc.)
DURATION_NUMBER: /(?i)(an|a|one|two|three|four|five|six|seven|eight|nine|ten|eleven|twelve|thirteen|fourteen|fifteen|sixteen|seventeen|eighteen|nineteen|twenty|thirty|forty|fifty|sixty|seventy|eighty|ninety)/

// Day suffix (th, rd, st, nd)
DAY_SUFFIX: /(?i)(th|rd|st|nd)/

FLOAT_NUMBER: /((\d+\.\d*|\.\d+)(e[-+]?\d+)?|\d+(e[-+]?\d+))/i  // from lark

// ----------------------
// PARSER RULES
// ----------------------
start: expression

expression: (single
          | list
          | range
          | unknown)+

range: single ("to" | "-") single

list: single ((","|"or")+ single)+ 
    | range ((","|"or")+ range)+

single: datetime 
       | duration
       | ambiguous

// Only add houronly to specific formats that immediately
// indicate this is an hour-only time.
datetime: date ("at" (time | houronly))?
        | date (time | houronly)
        | (time | houronly) date
        | (time | houronly) "on" date
        | date
        | time

date: month "/" day "/" year
    | month "/" dayoryear
    | month "-" day "-" year
    | month "-" dayoryear
    | datename
    | weekday
    | monthname day DAY_SUFFIX? (",")? year
    | monthname day DAY_SUFFIX
    | day DAY_SUFFIX
    | monthname dayoryear

// intentionally not allowing int-only time, so that single-integers can be
// classified as an ambiguous token (in case it's a month, day, year, etc.)
// However, that means to support single-integer (e.g., hour) times, we need
// to manually add them to the `datetime` rule above.
time: hour ":" minute meridiem?
    | hour ("o'clock")? meridiem
    | timename

duration: ("in"|"for")? duration_part (("and"|",")? duration_part)* ("ago")?
duration_part: duration_number (duration_unit | duration_unit_letter)
             | duration_numbername " " duration_unit
duration_number: FLOAT_NUMBER | INT
duration_numbername: DURATION_NUMBER+
duration_unit: DURATION_UNIT
duration_unit_letter: DURATION_UNIT_LETTER

weekday: WEEKDAY
monthname: MONTHNAME
datename: DATENAME
dayoryear: INT

day: INT
month: INT
year: INT

timename: TIMENAME
hour: INT
minute: INT
meridiem: MERIDIEM
houronly: INT

ambiguous: INT

unknown: /\S/